defineTest(projectGlobals) {
    DEFINES += QT_NO_CAST_FROM_ASCII
    DEFINES += QT_NO_CAST_TO_ASCII
    DEFINES += QT_NO_NARROWING_CONVERSIONS_IN_CONNECT
    # TODO: not sure what I want yet.
    # DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x051200
    DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x060000
    DEFINES += TOML_HEADER_ONLY=0
    export(DEFINES)

    unix {
        QMAKE_CXXFLAGS += -Werror
        QMAKE_CXXFLAGS += -Wall
        QMAKE_CXXFLAGS += -Wshadow
        QMAKE_CXXFLAGS += -Wextra
    }
    export(QMAKE_CXXFLAGS)

    CONFIG += c++17
    export(CONFIG)
}

defineTest(useLibMoebius) {
    INCLUDEPATH += $$SOURCE_TREE/src
    DEPENDPATH  += $$SOURCE_TREE/src

    libpath = $$BUILD_TREE/lib/
    LIBS += -L$$libpath
    LIBS += -lmoebius

    win32 {
        libname = libmoebius.a
        !win32-g++: libname = moebius.lib
        PRE_TARGETDEPS += $$libpath$$libname
        export(PRE_TARGETDEPS)
    }

    export(INCLUDEPATH)
    export(DEPENDPATH)
    export(LIBS)
}
