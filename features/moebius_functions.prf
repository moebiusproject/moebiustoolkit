defineTest(projectGlobals) {
    DEFINES += QT_NO_CAST_FROM_ASCII
    DEFINES += QT_NO_CAST_TO_ASCII
    DEFINES += QT_NO_NARROWING_CONVERSIONS_IN_CONNECT
    DEFINES += QT_DISABLE_DEPRECATED_BEFORE=0x051500
    export(DEFINES)

    unix {
        QMAKE_CXXFLAGS += -Werror
        QMAKE_CXXFLAGS += -Wall
        QMAKE_CXXFLAGS += -Wshadow
        QMAKE_CXXFLAGS += -Wextra
        QMAKE_CXXFLAGS += -Wno-error=deprecated-declarations
        clang:QMAKE_CXXFLAGS += -Wno-error=deprecated-enum-enum-conversion
        QMAKE_CXXFLAGS += -Wno-error=deprecated-enum-enum-conversion
    }
    export(QMAKE_CXXFLAGS)

    CONFIG += c++latest
    export(CONFIG)
}

defineTest(useLibMoebius) {
    INCLUDEPATH += $$SOURCE_TREE/src
    DEPENDPATH  += $$SOURCE_TREE/src

    libpath = $$BUILD_TREE/lib/
    LIBS += -L$$libpath
    LIBS += -lmoebius

    win32 {
        libname = libmoebius.a
        !win32-g++: libname = moebius.lib
        PRE_TARGETDEPS += $$libpath$$libname
        export(PRE_TARGETDEPS)
    }

    export(INCLUDEPATH)
    export(DEPENDPATH)
    export(LIBS)
}
